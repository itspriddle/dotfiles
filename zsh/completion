autoload -U compinit
compinit -i

# Automatically list choices on ambiguous completion.
setopt AUTO_LIST

# Cache completions
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$HOME/.zcompcache"

# Case insensitive completion
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# Force rehash of commands
zstyle ":completion:*:commands" rehash 1

# Setup colors
# Bold
zstyle ":completion:*:default" list-colors \
  "di=1;34;27:ln=1;36;27:so=1;35;27:pi=33;27:ex=1;32;27:bd=1;33;" \
  "27:cd=1;33;27:su=1;31;27:sg=1;31;27:tw=1;34;27:ow=1;34;27:"

# Non-bold
# zstyle ":completion:*:default" list-colors \
#   "di=0;34;27:ln=0;36;27:so=0;35;27:pi=33;27:ex=0;32;27:bd=0;33;" \
#   "27:cd=0;33;27:su=0;31;27:sg=0;31;27:tw=0;34;27:ow=0;34;27:"

zstyle ":completion:*" list-colors ""

# ssh/ping host completion
{
  local -a _ssh_hosts _ssh_users

  # Only complete hosts in ~/.ssh/config, not /etc/hosts.
  if [[ -f $HOME/.ssh/config ]]; then
    _ssh_hosts=($(awk '/^Host [a-z0-9.\-]+$/ { print $2 }' ~/.ssh/config))
  else
    _ssh_hosts=()
  fi

  # ssh host completion
  zstyle ':completion:*:(ssh|scp):*:hosts' hosts $_ssh_hosts

  # Disable completion on users with ssh/scp
  zstyle ':completion:*:(ssh|scp)*:users' users

  # ping host completion
  zstyle ':completion:*:ping:*' hosts google.com 10.0.1.1 ${_ssh_hosts[*]}
}

# Ignore _function files
zstyle ':completion:*:functions' ignored-patterns '_*'

# Complete $PWD/bin and $PWD/.bundle/bin
zstyle -e ':completion:*' command-path \
  'reply=($PWD/bin $PWD/.bundle/bin $path)'

# Kill/killall completion
zstyle ':completion:*:*:*:*:processes' command 'ps -u $USER -o pid,user,comm -w'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;36=0=01'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*' insert-ids single

zstyle ':completion:*:killall:*' command 'ps -u $USER -o comm'

# vim:set ft=zsh:
