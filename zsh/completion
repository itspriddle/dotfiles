autoload -U compinit
compinit -i

# Cache completions
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$HOME/.zcompcache"

# Case insensitive completion
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# Force rehash of commands
zstyle ":completion:*:commands" rehash 1

# ssh/ping host completion
{
  local -a _ssh_hosts _ssh_users

  # Only complete hosts in ~/.ssh/known_hosts, not /etc/hosts.
  # Parse lines where we connected on a non-standard port
  # (eg: [example.com]:2222,[192.168.1.1] ssh-rsa AAAAA)
  if [[ -f $HOME/.ssh/known_hosts ]]; then
    _ssh_hosts="$(<$HOME/.ssh/known_hosts)"
    _ssh_hosts=(${${${${${(f)${_ssh_hosts}}%%\ *}%,*}/]:[0-9]*}/\[})
  else
    _ssh_hosts=()
  fi

  # ssh host completion
  zstyle ':completion:*:(ssh|scp):*:hosts' hosts $_ssh_hosts

  # Disable completion on users with ssh/scp
  _ssh_users=()
  zstyle ':completion:*:(ssh|scp)*:users' users $_ssh_users

  # ping host completion
  zstyle ':completion:*:ping:*' hosts google.com 192.168.1.1 ${_ssh_hosts[*]}
}

# Custom completion for git
zstyle ':completion:*:*:git:*' user-commands \
  repl:'Open a repl' \
  sexytime:'Push branch to github and open a pull request'

# Ignore _function files
zstyle ':completion:*:functions' ignored-patterns '_*'

# Complete $PWD/bin and $PWD/.bundle/bin
zstyle -e ':completion:*' command-path \
  'reply=($PWD/bin $PWD/.bundle/bin $path)'

# vim:set ft=zsh:
