# Some systems' /etc/zshrc might have called this already
if ! type compinit &> /dev/null; then
  autoload -U compinit
  compinit -i
fi

# Automatically list choices on ambiguous completion.
setopt AUTO_LIST

# Don't beep on an ambiguous completion.
setopt NO_LIST_BEEP

# Cache completions
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$HOME/.zcompcache"

# Case insensitive completion
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# Force rehash of commands
zstyle ":completion:*:commands" rehash 1

# Setup colors
# Bold
zstyle ":completion:*:default" list-colors \
  "di=1;34;27:ln=1;36;27:so=1;35;27:pi=33;27:ex=1;32;27:bd=1;33;" \
  "27:cd=1;33;27:su=1;31;27:sg=1;31;27:tw=1;34;27:ow=1;34;27:"

# Non-bold
# zstyle ":completion:*:default" list-colors \
#   "di=0;34;27:ln=0;36;27:so=0;35;27:pi=33;27:ex=0;32;27:bd=0;33;" \
#   "27:cd=0;33;27:su=0;31;27:sg=0;31;27:tw=0;34;27:ow=0;34;27:"

zstyle ":completion:*" list-colors ""

# ssh/ping host completion
{
  local -a _ssh_hosts _ssh_users

  _ssh_hosts=()

  # Only complete hosts in ~/.ssh/config, not /etc/hosts.
  if [[ -f $HOME/.ssh/config ]]; then
    _ssh_hosts=($(
      awk '/^Host [A-Za-z0-9][A-Za-z0-9\.\- ]+$/ {
        sub(/^Host /, "", $0)
        gsub(" ", "\n", $0)
        print $0
      }' ~/.ssh/config ~/.ssh/config.d/*(N)
    ))
  fi

  if [[ -f "$HOME/.ssh/known_hosts" ]]; then
    _ssh_hosts+=($(
      awk '/^[^#]/ { gsub("\\[", "", $1); gsub("]:2222", "", $1); print $1 }' ~/.ssh/known_hosts | sort | uniq
    ))
  fi

  # ssh host completion
  zstyle ':completion:*:(ssh|scp):*:hosts' hosts $_ssh_hosts

  # Disable completion on users with ssh/scp
  zstyle ':completion:*:(ssh|scp)*:users' users

  # ping host completion
  zstyle ':completion:*:ping:*' hosts google.com 10.0.1.1 ${_ssh_hosts[*]}
}

# Ignore _function files
zstyle ':completion:*:functions' ignored-patterns '_*'

# Kill/killall completion
zstyle ':completion:*:*:*:*:processes' command 'ps -u $USER -o pid,user,comm -w'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;36=0=01'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*' insert-ids single

zstyle ':completion:*:killall:*' command 'ps -u $USER -o comm'

# Ignore completion on some files when there's an ambiguous match
zstyle ':completion:*:*:*:*:*files' ignored-patterns 'appspec.yml'

# vim: ft=zsh
