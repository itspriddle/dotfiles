#!/usr/bin/env zsh

# Use vcs_info to get git prompt
autoload -Uz vcs_info
autoload -Uz add-zsh-hook

zstyle ':vcs_info:*'      enable git
zstyle ':vcs_info:git*:*' get-revision true
zstyle ':vcs_info:git*:*' check-for-changes true
#zstyle ':vcs_info:git*:*' unstagedstr '' # %u
zstyle ':vcs_info:git*:*' stagedstr " âš¡"  # %c
zstyle ':vcs_info:git*'   actionformats " (%b%c|%a)"
zstyle ':vcs_info:git*'   formats       " (%b%c)"

josh_prompt_precmd() {
  local git_prompt dir_prompt host_prompt branch_color dir_color host_color

  case "$TERM" in
    xterm*|rxvt*)
      if [[ -n $SSH_CONNECTION ]]; then
        print -Pn "\e]0;%n@%M:%~\a"
      else
        print -Pn "\e]0;%n:%~\a"
      fi
      ;;
  esac

  vcs_info

  branch_color="green"
  dir_color="blue"
  host_color="yellow"

  if [[ -n $vcs_info_msg_0_ ]]; then
    git_prompt="%{$fg_bold[$branch_color]%}$vcs_info_msg_0_%{$terminfo[sgr0]%}"
  else
    git_prompt=""
  fi

  dir_prompt="%{$fg_bold[$dir_color]%}%3~%{$terminfo[sgr0]%}"

  if [[ -n $SSH_CONNECTION ]]; then
    host_prompt="%{$fg_bold[$host_color]%}%n@%M%{$terminfo[sgr0]%} "
  else
    host_prompt=""
  fi

  PS1="${host_prompt}${dir_prompt}${git_prompt} > "
}

add-zsh-hook precmd josh_prompt_precmd
