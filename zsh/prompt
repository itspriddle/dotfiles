#!/usr/bin/env zsh

# Use vcs_info to get git prompt
autoload -Uz vcs_info
autoload -Uz add-zsh-hook

zstyle ':vcs_info:*'      enable git
zstyle ':vcs_info:git*:*' get-revision true
zstyle ':vcs_info:git*:*' check-for-changes true
#zstyle ':vcs_info:git*:*' unstagedstr '' # %u
zstyle ':vcs_info:git*:*' stagedstr " âš¡"  # %c
zstyle ':vcs_info:git*'   actionformats " (%b%c|%a)"
zstyle ':vcs_info:git*'   formats       " (%b%c)"

josh_prompt_setup() {
  add-zsh-hook precmd josh_prompt_precmd
}

josh_prompt_precmd() {
  local git_prompt dir_prompt host_prompt

  case "$TERM" in
    xterm*|rxvt*)
      if [[ -n $SSH_CONNECTION ]]; then
        print -Pn "\e]0;%M@%n:%~\a"
      else
        print -Pn "\e]0;%n:%~\a"
      fi
      ;;
  esac

  vcs_info

  if [[ -n $vcs_info_msg_0_ ]]; then
    git_prompt="%{$fg_bold[green]%}$vcs_info_msg_0_%{$reset_color%}"
  else
    git_prompt=""
  fi

  dir_prompt="%{$fg_bold[blue]%}%3~%{$reset_color%}"

  if [[ -n $SSH_CONNECTION ]]; then
    host_prompt="%{$fg_bold[yellow]%}%n@%M%{$reset_color%} "
  else
    host_prompt=""
  fi

  PS1="${host_prompt}${dir_prompt}${git_prompt} > "
}

josh_prompt_setup
